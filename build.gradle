ext {
	projectVersion = '0.4.2-SNAPSHOT'
	projectGroup = 'io.codearte.accurest'
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'maven-publish'

	if (!projectVersion.contains('SNAPSHOT')) {
		apply from: "$rootDir/release.gradle"
	}

	group = projectGroup
	version = projectVersion

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	repositories {
		mavenLocal()
		mavenCentral()
	}

	//Dependencies in all subprojects - http://solidsoft.wordpress.com/2014/11/13/gradle-tricks-display-dependencies-for-all-subprojects-in-multi-project-build/
	task allDeps(type: DependencyReportTask) {}
	task allInsight(type: DependencyInsightReportTask) {}

	dependencies {
		compile localGroovy()
		testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
			exclude(group: 'org.codehaus.groovy')
		}
	}

	publishing {
		publications {
			maven(MavenPublication) {
				groupId projectGroup
				version projectVersion

				from components.java
			}
		}
	}
}

project(':accurest-core') {
	dependencies {
		compile 'org.slf4j:slf4j-api:[1.6.0,)'
		compile 'org.codehaus.plexus:plexus-utils:3.0.21'
		testCompile 'cglib:cglib-nodep:2.2'
		testCompile 'org.objenesis:objenesis:2.1'
	}
}

project(':accurest-converters') {
	dependencies {
		compile project(':accurest-core')
		compile 'org.apache.commons:commons-lang3:3.3.2'
		compile 'commons-io:commons-io:[2.4,)'
	}
}

project(':accurest-gradle-plugin') {
	dependencies {
		compile project(':accurest-core')
		compile project(':accurest-converters')
		compile gradleApi()
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}