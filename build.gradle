ext {
	projectVersion = '0.3.0-SNAPSHOT'
	projectGroup = 'io.codearte.accurest'
}

subprojects {
	apply plugin: 'groovy'
	apply plugin: 'maven-publish'

	if (!projectVersion.contains('SNAPSHOT')) {
		apply from: "$rootDir/release.gradle"
	}

	group = projectGroup
	version = projectVersion

	sourceCompatibility = 1.7
	targetCompatibility = 1.7

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {
		compile localGroovy()
		testCompile('org.spockframework:spock-core:0.7-groovy-2.0') {
			exclude(group: 'org.codehaus.groovy')
		}
	}

	publishing {
		publications {
			maven(MavenPublication) {
				groupId projectGroup
				version projectVersion

				from components.java
			}
		}
	}
}

project(':accurest-core') {
}

project(':accurest-converters') {
	dependencies {
		compile project(':accurest-core')
		compile 'org.apache.commons:commons-lang3:3.3.2'
	}
}

project(':accurest-gradle-plugin') {
	dependencies {
		compile project(':accurest-core')
		compile gradleApi()
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.2.1'
}